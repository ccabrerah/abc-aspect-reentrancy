// ----------------------- Aspects ------------------------

AspectDecl : ClassDecl ::= Modifiers
                           <ID:String>
                           BodyDecl*
                           [SuperClassAccess:Access]
                           Implements:Access*
                           [PerClause];


// ----------------------- Advice -------------------------

AdviceDecl : BodyDecl ::= Modifiers AdviceSpec PointcutExpr
                          Exception:Access* Block;

abstract AdviceSpec ::= Parameter:ParameterDeclaration*;
BeforeSpec : AdviceSpec;
AfterSpec : AdviceSpec;
AfterReturningSpec : AfterSpec ::= [ReturnParameter:ParameterDeclaration];
AfterThrowingSpec : AfterSpec ::= [ExceptionParameter:ParameterDeclaration];
AroundSpec : AdviceSpec ::= TypeAccess:Access;

abstract PerClause;
PerTarget : PerClause ::= PointcutExpr;
PerThis : PerClause ::= PointcutExpr;
PerCflow : PerClause ::= PointcutExpr;
PerCflowBelow : PerClause ::= PointcutExpr;
IsSingleton : PerClause;

Proceed : Access ::= Arg:Expr*;
PointcutAccess : Access ::= <ID:String>;
ImplicitParameterDeclaration : ParameterDeclaration;


// ----------------------- Declare (...) -------------------

DeclareParentsExtends : BodyDecl ::= Pattern TypeAccess:Access*;
DeclareParentsImplements : BodyDecl ::= Pattern TypeAccess:Access*;
DeclareWarning : BodyDecl ::= PointcutExpr <Message:String>;
DeclareError : BodyDecl ::= PointcutExpr <Message:String>;
DeclareSoft : BodyDecl ::= Access PointcutExpr;
DeclarePrecedence : BodyDecl ::= Pattern*;


// ----------------------- Intertype declarations ----------

//IntertypeMethodDecl : MethodDecl ::= TargetType:Access;
//IntertypeConstructorDecl : ConstructorDecl ::= HostType:Access;
//IntertypeFieldDeclaration : FieldDeclaration ::= HostType:Access;


// ----------------------- Pointcuts -----------------------

PointcutDecl : BodyDecl ::= Modifiers
                            <ID:String>
                            Parameter:ParameterDeclaration*
                            /Implicit:ParameterDeclaration*/
                            PointcutExpr;

abstract PointcutExpr;
abstract BinaryPointcutExpr : PointcutExpr ::= Lhs:PointcutExpr Rhs:PointcutExpr;
OrPointcutExpr : BinaryPointcutExpr;
AndPointcutExpr : BinaryPointcutExpr;

NegPointcutExpr : PointcutExpr ::= PointcutExpr;

CallPointcutExpr : PointcutExpr ::= Pattern:MemberPattern;
ExecutionPointcutExpr : PointcutExpr ::= Pattern:MemberPattern;

InitializationPointcutExpr : PointcutExpr ::= Pattern:ConstructorPattern;
PreInitializationPointcutExpr : PointcutExpr ::= Pattern:ConstructorPattern;
StaticInitializationPointcutExpr : PointcutExpr ::= Pattern:Pattern;

GetPointcutExpr : PointcutExpr ::= Pattern:FieldPattern;
SetPointcutExpr : PointcutExpr ::= Pattern:FieldPattern;

HandlerPointcutExpr : PointcutExpr ::= Pattern:Pattern;
AdviceExecutionPointcutExpr : PointcutExpr;
WithinPointcutExpr : PointcutExpr ::= Pattern:Pattern;
WithinCodePointcutExpr : PointcutExpr ::= Pattern:MemberPattern;

CflowPointcutExpr : PointcutExpr ::= Pointcut:PointcutExpr;
CflowBelowPointcutExpr : PointcutExpr ::= Pointcut:PointcutExpr;

IfPointcutExpr : PointcutExpr ::= Expr;

ThisPointcutExpr : PointcutExpr ::= Pattern:BindingPattern;
TargetPointcutExpr : PointcutExpr ::= Pattern:BindingPattern;
ArgsPointcutExpr : PointcutExpr ::= Pattern:BindingPattern*;

NamedPointcutExpr : PointcutExpr ::= Name:Access Pattern:BindingPattern*;

EmptyPointcutExpr : PointcutExpr;



// ----------------------- NamePatterns ------------------
abstract Pattern;
abstract BinaryPattern : Pattern ::= Lhs: Pattern Rhs: Pattern;
AndPattern : BinaryPattern;
OrPattern : BinaryPattern;
NegPattern : Pattern ::= Pattern;
UniversalNegPattern : Pattern ::= Pattern;

abstract NamePattern : Pattern;
SimpleNamePattern : NamePattern ::= <Pattern:String>;
DotNamePattern : NamePattern ::= Lhs:NamePattern Rhs:SimpleNamePattern;
DotDotNamePattern : NamePattern ::= Lhs:NamePattern Rhs:SimpleNamePattern;
TypeDotNamePattern : NamePattern ::= Lhs:Pattern Rhs:SimpleNamePattern;

ExplicitTypeNamePattern : NamePattern ::= Access;
SubtypeNamePattern : NamePattern ::= Pattern:NamePattern;
ArraytypeNamePattern : NamePattern ::= Pattern:NamePattern Dims*;

// ---------------------- Modifier patterns ---------------
ModifierPattern ::= Modifier;
NegModifierPattern : ModifierPattern;


// ---------------------- Member patterns ----------------
abstract MemberPattern;
MethodPattern : MemberPattern ::= ModifierPattern* ReturnTypePattern:Pattern MemberNamePattern:NamePattern FormalPattern* ThrowsPattern:Pattern*;

ConstructorPattern : MemberPattern ::= ModifierPattern*  MemberNamePattern:NamePattern FormalPattern* ThrowsPattern:Pattern*;

FieldPattern : MemberPattern ::= ModifierPattern* TypePattern:Pattern MemberNamePattern:NamePattern;

abstract FormalPattern;
WildcardFormalPattern : FormalPattern;
ConcreteFormalPattern : FormalPattern ::= Pattern;
 
// -----------------------------------------------------------

abstract BindingPattern;
StarBindingPattern : BindingPattern;
DotDotBindingPattern : BindingPattern;
NameBindingPattern : BindingPattern ::= Pattern;

